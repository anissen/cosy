// https://adventofcode.com/2020/day/2

foreign fn read_lines(path Str) Array Str
foreign fn string_to_number(input Str) Num

mut input Array Str
let test_mode = true
if test_mode {
    input = ['1-3 a: abcde','1-3 b: cdefg','2-9 c: ccccccccc']
} else {
    input = read_lines('test/examples/advent-of-code-2020/data/day2.txt')
}

struct Password {
    let min Num
    let max Num
    let char Str
    let pass Array Str
}

fn parse(str Str) Password {
    let parts = str.split(' ')  // ['1-3', 'a:', 'abcde'] from '1-3 a: abcde'
	let min_max = parts[0].split('-') // [1, 3] from '1-3
    let min = string_to_number(min_max[0])
    let max = string_to_number(min_max[1])
    let char = parts[1].split(':')[0] // 'a' from 'a:'
    let pass = parts[2]
    return Password { min = min, max = max, char = char, pass = pass.split('') }
}

fn validate_part_1(p Password) {
    let char_count = p.pass.count(fn (char Str) {
        return char == p.char
    })
    return char_count >= p.min and char_count <= p.max
}

fn validate_part_2(p Password) {
    let first_char_matches = p.pass[p.min - 1] == p.char
    let second_char_matches = p.pass[p.max - 1] == p.char
    if first_char_matches and second_char_matches {
        return false
    }
    return first_char_matches or second_char_matches
}

let parsed_input = input.map(parse)

// Part 1
{
    let valid_count_part_1 = parsed_input.filter(validate_part_1)
    print '# PART 1: {valid_count_part_1.length} valid passwords'
}

// Part 2
{
    let valid_count_part_2 = parsed_input.filter(validate_part_2)
    print '# PART 2: {valid_count_part_2.length} valid passwords'
}