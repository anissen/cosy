// Adapted from https://github.com/snekkylang/snekky/blob/master/examples/brainfuck.snek
foreign fn string_from_char_code(c Num) Str

fn brainfuck(program Str, input_str Str) {
    mut input = input_str.split('')
    mut mem Array Num = []
    var mem_size = 1000
    for 0..mem_size {
        mem.push(0)
    }
    mut ip = 0
    mut dp = 0
    mut out = ''

    for ip < program.length {
        var code = program.char_at(ip)
        if code == '>' {
            dp += 1
        } else if code == '<' {
            dp -= 1
        } else if code == '+' {
            mem[dp] += 1
        } else if code == '-' {
            mem[dp] -= 1
        } else if code == '.' {
            out += string_from_char_code(mem[dp])
        } else if code == ',' {
            mem[dp] = input.shift().char_code_at(0)
        } else if code == '[' {
            if mem[dp] == 0 {
                mut nc = 1
                for nc > 0 {
                    ip += 1
                    if program.char_at(ip) == '[' {
                        nc += 1
                    } else if program.char_at(ip) == ']' {
                        nc -= 1
                    }
                }
            }
        } else if code == ']' {
            if mem[dp] != 0 {
                mut nc = 1
                for nc > 0 {
                    ip -= 1
                    if program.char_at(ip) == ']' {
                        nc += 1
                    } else if program.char_at(ip) == '[' {
                        nc -= 1
                    }
                }
            }
        }
        ip += 1
    }

    print out
}

// Prints 'Hello World!'
brainfuck('++++++++[>++++[>++>+++>+++>+<<<<-]>+>+>->>+[<]<-]>>.>---.+++++++..+++.>>.<-.<.+++.------.--------.>>+.>++.', '')

// Calculates the sum of 2 and 3
brainfuck(',>,[<+>-]<------------------------------------------------.', '23')