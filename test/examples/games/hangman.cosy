foreign fn read_input() Str

fn get_secret_word() {
    let words = ['WOBNIAR','EMOSEWA','SESSALGNUS','TUCREPAP','ENIHSNUS','DLEIFRAG','ELCYCIB','RETUPMOC','NOLEMRETAW','HTOLCELBAT']
    let secret = words.get(words.length * random())
    mut r = ''
    for c in secret.split('') {
        r = c + r
    }
    return r
}

let secret_word = get_secret_word()
let word_to_guess = secret_word.split('')
mut letters_guessed Array Str = []
mut wrong_guesses_remaining = 5

fn get_display_word() {
    mut word_display = ''
    for l in word_to_guess {
        if letters_guessed.contains(l) {
            word_display += l
        } else {
            word_display += '_'
        }
        word_display += ' '
    }
    return word_display
}

fn has_won() {
    for l in word_to_guess {
        if !letters_guessed.contains(l) {
            return false
        }
    }
    return true
}

fn hangman() {
    for {
        if wrong_guesses_remaining <= 0 {
            print 'Game over: You lost!\nThe correct word was ' + secret_word
            break
        }

        print 'You need to guess "' + get_display_word() + '"!'
        print 'Wrong guesses remaining: ' + wrong_guesses_remaining
        print '-------------------------'
        print 'Guess: '
        let letter = read_input()

        if letter.length != 1 or !['A','B','C','D','E','F','G','H','I','J','K','L','M','N','O','P','Q','R','S','T','U','V','W','X','Y','Z'].contains(letter) {
            print 'Input must be an uppercased letter between A and Z!\n'
            continue
        }

        if letters_guessed.contains(letter) {
            print 'You already guessed that!\n'
            wrong_guesses_remaining -= 1
            continue
        }

        letters_guessed.push(letter)

        if word_to_guess.contains(letter) {
            print 'Correct!\n'
            if has_won() {
                print 'The correct word is ' + secret_word + '\nGame over: You won!'
                break
            }
        } else {
            print 'Wrong guess!\n'
            wrong_guesses_remaining -= 1
        }
    }
}

print 'Welcome to Hangman!\n'
hangman()
print '\nThanks for playing :)'
